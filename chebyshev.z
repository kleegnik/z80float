; function approximations by Chebyshev polynomials
; (evaluated by Horner's method)

    org  0

start:
    di
    ld   sp,2048

; HLDE contains float (x) on entry.
; C:
;  0 -> exp
;  1 -> sin

    ld   a,c
    or   a
    jr   z,chexp
    cp   1
    jp   z,chsin
    ld   hl,0
    ld   d,h
    ld   e,l          ; invalid
    halt


; -------------------------------------------
;     EXP(x)
;
; 8th order Chebyshev polynomial for exp(x)
; in range -1 <= x <= 1
;
; -> around 15000 clock cycles. Unrolled loop version isn't much faster.

;   e = coeff[ncoeff-1]
;   for n in range(ncoeff-2,-1,-1):
;       e = e*x+coeff[n]

chexp:
    ld   (xvar),hl
    ld   (xvar+2),de          ; save x
    ld   hl,coeff6+3          ; last byte of penultimate coefficient
    ld   (coeffptr),hl
    ld   hl,$3956
    ld   de,$A4BD             ; e = final coefficient = 0.00020470

    ld   b,7
chexp1:
    push bc
    exx
    ld   hl,(xvar)
    ld   de,(xvar+2)
    exx
    call flmult               ; HLDE = e*x
    exx
    ld   bc,(coeffptr)
    ld   a,(bc)
    ld   e,a
    dec  bc
    ld   a,(bc)
    ld   d,a
    dec  bc
    ld   a,(bc)
    ld   l,a
    dec  bc
    ld   a,(bc)
    ld   h,a
    dec  bc                   ; HLDE = coefficient
    ld   (coeffptr),bc
    exx
    call fladd                ; e = e*x + coeff

    pop  bc
    djnz chexp1

    halt

;
xvar:       defw 0,0
coeffptr:   defw 0
coeff:
    defb $3F,$7F,$FF,$FD        ; 0.99999980
    defb $3F,$7F,$FF,$FE        ; 0.99999990
    defb $3F,$00,$00,$6B        ; 0.50000635
    defb $3E,$2A,$AB,$03        ; 0.16666799
    defb $3D,$2A,$89,$79        ; 0.04163501
    defb $3C,$08,$74,$AE        ; 0.00832860
coeff6:
    defb $3A,$BC,$A5,$E3        ; 0.00143927



; ------------------------------------------
;      SIN(x)
;
; 7th order Chebyshev polynomial:
; sin(x) ~= x - 0.166666546*x^3 + 0.00833216076*x^5 - 0.000195152832*x^7
;
;x2 = x*x
;sin(x) = x*(1+x2*(a + x2*(b + c*x2)))
;
; [= x-a*pow(x,3)+b*pow(x,5)-c*pow(x,7)]
;
; -> around 15000 clock cycles.

chsin:
; HLDE contains float (x) on entry: 0<x<1.57.. (at least pi/2)

    push hl
    push de                   ; save x
    push hl
    push de
    exx
    pop  de
    pop  hl
    exx
    call flmult               ; HLDE = x*x
    push hl
    push de
    push hl
    push de                   ; save x*x twice

    exx
    ld   hl,$B94C
    ld   de,$A1F0             ; -0.000195152832
    exx

    call flmult

    exx
    ld   hl,$3C08
    ld   de,$839D             ; 0.00833216076
    exx

    call fladd

    exx
    pop  de
    pop  hl
    exx

    call flmult

    exx
    ld   hl,$BE2A
    ld   de,$AAA3             ; -0.166666546
    exx

    call fladd
    exx
    pop  de
    pop  hl
    exx
    call flmult

    exx
    ld   hl,$3F80
    ld   de,$0000             ; 1.0
    exx

    call fladd
    exx
    pop  de
    pop  hl
    exx
    call flmult
    halt


; ------------------------------------------
include arithmetic.z

; end
