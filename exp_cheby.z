; 8th order Chebyshev polynomial for exp(x)
; in range -1 <= x <= 1
;
; -> around 15000 clock cycles.


;
;	e = coeff[ncoeff-1]
;	for n in range(ncoeff-2,-1,-1):
;		e = e*x+coeff[n]

   org 0

start:
   di
   ld sp,2048

; HLDE contains float (x) on entry.
	ld (xvar),hl
	ld (xvar+2),de	; save x
	ld hl,coeff6+3	; last byte of penultimate coefficient
	ld (coeffptr),hl
	ld hl,$3956
	ld de,$A4BD		; e = final coefficient = 0.00020470

	ld b,7
chebyev:
	push bc
	exx
	ld hl,(xvar)
	ld de,(xvar+2)
	exx
	call flmult		; HLDE = e*x
	exx
	ld bc,(coeffptr)
	ld a,(bc)
	ld e,a
	dec bc
	ld a,(bc)
	ld d,a
	dec bc
	ld a,(bc)
	ld l,a
	dec bc
	ld a,(bc)
	ld h,a
	dec bc			; HLDE = coefficient
	ld (coeffptr),bc
	exx
	call fladd		; e = e*x + coeff

	pop bc
	djnz chebyev

	halt

;
xvar: defw 0,0
coeffptr: defw 0
coeff:
	defb $3F,$7F,$FF,$FD		; 0.99999980
	defb $3F,$7F,$FF,$FE		; 0.99999990
	defb $3F,$00,$00,$6B		; 0.50000635
	defb $3E,$2A,$AB,$03		; 0.16666799
	defb $3D,$2A,$89,$79		; 0.04163501
	defb $3C,$08,$74,$AE		; 0.00832860
coeff6:
	defb $3A,$BC,$A5,$E3		; 0.00143927

; ------------------------------------------
include arithmetic.z

; end
